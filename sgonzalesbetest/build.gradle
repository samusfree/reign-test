plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.4'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'org.graalvm.buildtools.native' version '0.9.28'
	id 'jacoco'
}

group = 'com.reign.sgonzales.betest'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'
	implementation group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.11.5'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-impl', version: '0.11.5'
	runtimeOnly group: 'io.jsonwebtoken', name: 'jjwt-jackson', version: '0.11.5'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.2'
	testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.11.0'
	testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '4.11.0'
}

tasks.named('test') {
	jvmArgs = [
			'-XX:+EnableDynamicAgentLoading',
	]
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacoco {
	toolVersion = "0.8.12"
}

jacocoTestCoverageVerification {
	afterEvaluate {
		classDirectories.setFrom files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					"com/reign/sgonzales/betest/configuration/**",
					"com/reign/sgonzales/betest/entities/**",
					"com/reign/sgonzales/betest/enums/**",
					"com/reign/sgonzales/betest/integration/**",
					"com/reign/sgonzales/betest/data/repository/**",
					"com/reign/sgonzales/betest/security/TokenUserDetails.class",
					"com/reign/sgonzales/betest/dto/**",
					"com/reign/sgonzales/betest/Application.class"
			])
		})
	}
	violationRules {
		rule {
			limit {
				minimum = 0.8
			}
		}
	}
}

check.dependsOn jacocoTestCoverageVerification

nativeTest {
	finalizedBy jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	afterEvaluate {
		classDirectories.setFrom files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					"com/reign/sgonzales/betest/configuration/**",
					"com/reign/sgonzales/betest/entities/**",
					"com/reign/sgonzales/betest/enums/**",
					"com/reign/sgonzales/betest/integration/**",
					"com/reign/sgonzales/betest/data/repository/**",
					"com/reign/sgonzales/betest/security/TokenUserDetails.class",
					"com/reign/sgonzales/betest/dto/**",
					"com/reign/sgonzales/betest/Application.class"
			])
		})
	}
}
